//file:noinspection GroovyAssignabilityCheck
plugins {
	id 'java'
	id 'org.springframework.boot' version '3.1.7'
	id 'io.spring.dependency-management' version '1.1.3'
	id "org.owasp.dependencycheck" version '9.0.7'
	id 'jacoco'
	id 'maven-publish'
}

group = 'com.example'
version = '0.4.1'
sourceCompatibility = '17'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.boot:spring-boot-starter-cache' // adding spring boot cache
	implementation 'org.springframework.boot:spring-boot-starter-actuator' // adding spring boot actuator
	implementation 'com.mysql:mysql-connector-j:8.2.0'
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'com.h2database:h2:2.2.222'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testImplementation 'io.rest-assured:rest-assured:5.3.2' // added for integration testing instead of using TestRestTemplate
	implementation 'org.yaml:snakeyaml:2.2' // updated to 2.2 since spring boot uses older version which has a vulnerability
	implementation 'com.github.ben-manes.caffeine:caffeine:3.1.8' //added caffeine for more control of the spring cache
	implementation 'org.shredzone.commons:commons-suncalc:3.8' // added for calculating sunrise and sunset times
	implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.1'
	implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.1'
	implementation 'com.fasterxml.jackson.core:jackson-core:2.17.1'
	implementation 'com.fasterxml.jackson.module:jackson-module-parameter-names:2.17.1'

}

test {
	useJUnitPlatform{
		failFast = true
	}

	testLogging {
		// Test logging - standardOut, started, passed, skipped, failed
		events "passed", "skipped", "failed"
	}
	afterSuite { desc, result -> // prints the results of the test
		if (!desc.parent) { // will match the outermost suite

			// Printing the results of the test - from https://stackoverflow.com/questions/3963708/gradle-how-to-display-test-results-in-the-console-in-real-time
			def output = "Results: ${result.resultType} (${result.testCount} tests, ${result.successfulTestCount} passed, ${result.failedTestCount} failed, ${result.skippedTestCount} skipped)"
			def startItem = '|  ', endItem = '  |'
			def repeatLength = startItem.length() + output.length() + endItem.length()
			println('\n' + ('-' * repeatLength) + '\n' + startItem + output + endItem + '\n' + ('-' * repeatLength))

			// Adding a link to the Gradle report if all the tests passed, since gradle already does this for failed tests
			// Having to format the file path to a URI to be able to open it in the browser and replace the file:/ with file:/// to make it work
			if (result.failedTestCount == 0) {
				def reportFile = new File("${project.buildDir}/reports/tests/test/index.html")
				def reportUri = reportFile.toURI().toString().replaceFirst("file:/", "file:///")
				println("All tests passed, see the report at: ${reportUri}")
			}
		}
	}
	finalizedBy jacocoTestReport // report is generated after tests run
}

tasks.register("testIntegration", Test) {
	useJUnitPlatform {
		failFast = true
		includeTags("integration")
	}
	testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.register("testSystem", Test) {
	useJUnitPlatform {
		failFast = true
		includeTags("system")
	}
	testLogging {
		events "passed", "skipped", "failed"
	}
}

tasks.register("testUnit", Test) {
	useJUnitPlatform {
		failFast = true
		includeTags("unit")
	}
	testLogging {
		events "passed", "skipped", "failed"
	}

}


jacocoTestReport {
	dependsOn test // tests are run before generating the report
	reports {
		xml.required = true
	}
}

bootJar {
	archiveFileName = "${archiveBaseName.get()}.${archiveExtension.get()}"
}

jar {
	enabled = false
}

publishing {
	repositories {
		maven {
			name = "GitHubPackages"
			url = "https://maven.pkg.github.com/knottem/weatherapi"
			credentials {
				username = System.getenv("GITHUB_ACTOR")
				password = System.getenv("GITHUB_TOKEN")
			}
		}
	}
}

tasks.register('printVersion') {
	doLast {
		println project.version
	}
}